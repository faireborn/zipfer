const Zipfer = @This();

const std = @import("std");
const Allocator = std.mem.Allocator;
const ArrayList = std.ArrayList;
const ArenaAllocator = std.heap.ArenaAllocator;
const Dir = std.fs.Dir;
const File = std.fs.File;

vocab: ArrayList([]const u8),
unk: usize,
token_freq: std.StringHashMap(usize),
allocator: Allocator,
arena: ArenaAllocator,

pub fn init(allocator: Allocator) Zipfer {
    return .{
        .vocab = .empty,
        .unk = 0,
        .token_freq = std.StringHashMap(usize).init(allocator),
        .allocator = allocator,
        .arena = ArenaAllocator.init(allocator),
    };
}

pub fn deinit(self: *Zipfer) void {
    self.arena.deinit();

    self.vocab.deinit(self.allocator);
    self.token_freq.deinit();
}

pub fn loadVocab(self: *Zipfer, file: File) !void {
    var file_buffer: [1024]u8 = undefined;
    var reader = file.reader(&file_buffer);

    while (try reader.interface.takeDelimiter('\n')) |line| {
        // Get only first column token
        var it = std.mem.splitAny(u8, line, "\t\n \r");
        if (it.next()) |token| {
            if (token.len == 0) continue;

            // Insert a token into the vocab
            const new_token = try Allocator.dupe(self.arena.allocator(), u8, token);
            try self.vocab.append(self.allocator, new_token);
        }
    }
}

pub fn count(self: *Zipfer, file: File) !void {
    // Initialize hash map
    for (self.vocab.items) |token| {
        try self.token_freq.put(token, 0);
    }

    var file_buffer: [1024]u8 = undefined;
    var reader = file.reader(&file_buffer);

    while (try reader.interface.takeDelimiter('\n')) |line| {
        var it = std.mem.splitAny(u8, line, " ");
        while (it.next()) |token| {
            if (token.len == 0) continue;

            if (self.token_freq.getPtr(token)) |ptr| {
                ptr.* += 1;
            } else {
                self.unk += 1;
            }
        }
    }
}

test "init deinit" {
    var zipfer = Zipfer.init(std.testing.allocator);
    defer zipfer.deinit();
}

test "load vocab" {
    var zipfer = Zipfer.init(std.testing.allocator);
    defer zipfer.deinit();

    var tmp = std.testing.tmpDir(.{});
    defer tmp.cleanup();

    try tmp.dir.writeFile(.{ .sub_path = "test.vocab", .data = 
        \\hello,
        \\world!
        \\ğŸ£
        \\ğŸ¤—
    });

    const file = try tmp.dir.openFile("test.vocab", .{ .mode = .read_only });
    defer file.close();

    try zipfer.loadVocab(file);

    const vocab = zipfer.vocab.items;
    try std.testing.expect(std.mem.eql(u8, "hello,", vocab[0]));
    try std.testing.expect(std.mem.eql(u8, "world!", vocab[1]));
    try std.testing.expect(std.mem.eql(u8, "ğŸ£", vocab[2]));
    try std.testing.expect(std.mem.eql(u8, "ğŸ¤—", vocab[3]));
}

test "count" {
    var zipfer = Zipfer.init(std.testing.allocator);
    defer zipfer.deinit();

    var tmp = std.testing.tmpDir(.{ .iterate = true });
    defer tmp.cleanup();

    try tmp.dir.writeFile(.{ .sub_path = "test.vocab", .data = 
        \\hello,
        \\world!
        \\ğŸ£
        \\ğŸ¤—
    });

    try tmp.dir.writeFile(.{ .sub_path = "input.txt", .data = 
        \\hello, ğŸ¤— world! ğŸ£ ğŸ£
        \\hello, ğŸ£ ğŸ¤—
        \\world! ğŸ£ world!
        \\world! ğŸ¤—
        \\world! ğŸ£ hello,
        \\hello, world! ğŸ£
        \\happy! ğŸ£ goodbye!
    });

    const vocab_file = try tmp.dir.openFile("test.vocab", .{ .mode = .read_only });
    defer vocab_file.close();

    const input_file = try tmp.dir.openFile("input.txt", .{ .mode = .read_only });
    defer input_file.close();

    try zipfer.loadVocab(vocab_file);
    try zipfer.count(input_file);

    try std.testing.expectEqual(4, zipfer.token_freq.get("hello,").?);
    try std.testing.expectEqual(6, zipfer.token_freq.get("world!").?);
    try std.testing.expectEqual(7, zipfer.token_freq.get("ğŸ£").?);
    try std.testing.expectEqual(3, zipfer.token_freq.get("ğŸ¤—").?);
    try std.testing.expectEqual(2, zipfer.unk);
}
